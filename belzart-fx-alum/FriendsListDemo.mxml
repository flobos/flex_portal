<?xml version="1.0" encoding="utf-8"?>
<mx:Application  xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" creationComplete="init();">
	<mx:Script>
		<![CDATA[
			
			import com.facebook.graph.FacebookDesktop;
			import com.facebook.graph.controls.Distractor;
			import com.facebook.graph.net.FacebookRequest;
			import com.facebook.graph.utils.FacebookDataUtils;
			
			import mx.collections.ArrayList;
			import mx.controls.Image;
			import mx.core.UIComponent;
			
			import spark.components.TextArea;
			import spark.components.Window;
			import spark.layouts.VerticalLayout;
			
			protected var friendsModel:FriendsModel;
			protected var win:Window;
			
			protected static const APP_ID:String = "YOUR_APP_ID"; //Place your application id here
			
			protected function init():void {				
				loginBtn.label = 'Login';
				loginBtn.enabled = true;
				showDetailsBtn.visible = false;
				friendsList.dataProvider = new ArrayList();				
				friendsList.itemRenderer = new FriendsListRenderer();
				
				FacebookDesktop.manageSession = false;
				FacebookDesktop.init(APP_ID);
			}
			
			protected function login():void {
				FacebookDesktop.login(handleLogin);
			}
		
			protected function loadFriends():void {
				friendsModel = new FriendsModel();
				friendsList.dataProvider = friendsModel.dataProvider;
				
				friendsModel.load();
			}
			
			protected function handleLogin(response:Object, fail:Object):void {
				if (!response) { return; }
				loginBtn.label = 'You are logged in.';
				loginBtn.enabled = false;
				
				loadFriends();
			}
			
			protected function showDetails():void {
				FacebookDesktop.api('/'+friendsList.selectedItem.id, handleDetailsLoad);
			}
			
			protected function handleDetailsLoad(response:Object, fail:Object):void {
				var ta:TextArea = new TextArea();
				ta.percentHeight = 100;
				ta.percentWidth = 100;
				
				var textToDisplay:Array = [];
				var d:Object = response;
				for (var n:String in d) {
					var displayValue:Object = d[n];
					switch (n) {
						case 'updated_time':
							displayValue = FacebookDataUtils.stringToDate(displayValue as String); break;
						case 'work':
						case 'hometown':
						case 'location':
							displayValue = objectToString(displayValue); break;
						case 'education':
							displayValue = arrayToString(displayValue as Array); break;
					}
					textToDisplay.push(n + ': ' + displayValue);
				}
				
				ta.text = textToDisplay.join('\n');
				
				var img:Image = new Image();
				img.addEventListener(Event.COMPLETE, onImageReady, false, 0, true);
				img.source = FacebookDesktop.getImageUrl(d.id, 'large');
				
				var distractor:Distractor = new Distractor();
				distractor.text = 'loading';
				var distractorContainer:UIComponent = new UIComponent();
				distractorContainer.addChild(distractor);
				
				var hGroup:HGroup = new HGroup();
				hGroup.percentWidth = 100;
				hGroup.percentHeight = 100;
				hGroup.addElement(img);
				hGroup.addElement(ta);
				
				win = new Window();
				win.width = 600;
				win.height = 350;
				win.addElement(hGroup);
				win.addElement(distractorContainer);
				distractor.x = win.width - distractor.width >> 1;
				distractor.y = win.height - distractor.height >> 1;
				win.open();
			}
			
			protected function onImageReady(event:Event):void {
				win.removeElementAt(win.numChildren);
			}
			
			protected function objectToString(value:Object):String {
				var arr:Array = [];
				for (var n:String in value) {
					arr.push(n + ': ' + value[n]);
				}
				return '\n\t' + arr.join('\n\t');
			}
			
			protected function arrayToString(value:Array):String {
				var arr:Array = [];
				var l:uint = value.length;
				for (var i:uint=0;i<l;i++) {
					arr.push(objectToString(value[i]));
				}
				
				return arr.join('\n');
			}
			
			protected function handleListChange():void {
				showDetailsBtn.visible = true;
				showDetailsBtn.label = "Show details for " + friendsList.selectedItem.name;
			}
			
		]]>
	</mx:Script>
	
	<mx:HGroup width="100%">
		<mx:Button id="showDetailsBtn" visible="false" click="showDetails()" />
		<mx:Spacer width="100%" />
		<mx:Button id="loginBtn" label="Login" click="login()" />		
	</mx:HGroup>
	<mx:List id="friendsList" width="100%" height="100%" change="handleListChange()" />
	
</mx:Application>
